<?xml version="1.0" encoding="utf-8"?>
<!-- 
license-start

Copyright (C) 2008 - 2013 Crispico Software, <http://www.crispico.com/>.

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation version 3.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details, at <http://www.gnu.org/licenses/>.

license-end
-->

<!--- 
@author Cristina Brinza 
-->

<ns:BasicViewContent xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:ns="org.flowerplatform.flexutil.view_content_host.*"
		 creationComplete="creationCompleteHandler(event)">
		
	<fx:Script>
		<![CDATA[			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			
			import spark.events.IndexChangeEvent;
			
			import org.flowerplatform.flex_client.core.CorePlugin;
			import org.flowerplatform.flex_client.core.editor.remote.Node;
			import org.flowerplatform.flex_client.team.git.GitConstants;
			import org.flowerplatform.flex_client.team.git.remote.GitBranch;
						
			[Bindable]
			public var branches:ArrayCollection;
			
			[Bindable]
			public var node:Node;
						
			private const CREATE_BRANCH_ERROR:String = "Create Branch Error!";
			
			override protected function creationCompleteHandler(event:FlexEvent):void {
				super.creationCompleteHandler(event);				
				
				mergeUpstreamRadioButton.selected = true;
				mergeUpstreamRadioButton.enabled = false;
				
				rebaseCommitsRadioButton.selected = false;
				rebaseCommitsRadioButton.enabled = false;
				
				checkoutBranchCheckBox.selected = true;

				CorePlugin.getInstance().serviceLocator.invoke("GitService.getBranches", [node.nodeUri], getBranchesHandler); 
			}
			
			protected function getBranchesHandler(serverBranches:ArrayCollection):void {
				/* populate branches */
				branches = serverBranches;
				
				/* find the selected index */
				for each (var branch:Object in serverBranches) {
					if (String(branch.branchName) == node.properties.fullName) {
						selectBranch.selectedItem = branch;
						checkState(branch);
						break;
					}
				}
				
				selectBranch.dataProvider = branches;
			}
			
			protected function configureUpstreamClickHandler(event:MouseEvent):void {
				/* enable radio buttons */
				mergeUpstreamRadioButton.enabled = !mergeUpstreamRadioButton.enabled;
				rebaseCommitsRadioButton.enabled = !rebaseCommitsRadioButton.enabled;
			}
					
			override protected function okHandler(event:Event = null):void {	
				var track:Boolean;
				var setUpstream:Boolean;
				var checkoutBranch:Boolean;
				var configureUpstream:Boolean;
				
				/* happens on ok */
				if (branchNameTextInput.text == "") {
					Alert.show("Please choose a name for the new branch", CREATE_BRANCH_ERROR);
				} else {					
					/* if the node is a tag, don't configure upstream in createBranch */
					configureUpstream = configureUpstreamGroup.visible;
					/* Merge upstream commits into local branch */
					track = configureUpstreamCheckBox.selected && mergeUpstreamRadioButton.selected;
					/* Rebase commits of local branch onto upstream */
					setUpstream = configureUpstreamCheckBox.selected && rebaseCommitsRadioButton.selected;
					/* Checkout new Branch */
					checkoutBranch = checkoutBranchCheckBox.selected;
										
					/* send to server the parent */
					var parentUri:String;
					if (node.parent.type == GitConstants.GIT_LOCAL_BRANCHES_TYPE) {
						parentUri = node.parent.nodeUri;
					} else {
						for each (var child:Node in node.parent.parent.children) {
							if (child.type == GitConstants.GIT_LOCAL_BRANCHES_TYPE) {
								parentUri = child.nodeUri;
							}
						}
						
					}
					CorePlugin.getInstance().serviceLocator.invoke("GitService.createBranch", [parentUri, branchNameTextInput.text, selectBranch.selectedItem.branchName, configureUpstream, track, setUpstream, checkoutBranch]);
				}
			}
			
			protected function selectBranchChangeHandler(event:IndexChangeEvent):void {				
				var selectedItem:Object = branches.getItemAt(selectBranch.selectedIndex);
			
				checkState(selectedItem);
			}
			
			/* determine which components will be visible / enabled / selected and which not */
			protected function checkState(selectedItem:Object):void {
				if (selectedItem.branchType == GitConstants.GIT_TAG_TYPE) {
					configureUpstreamGroup.visible = false;
				} else if (selectedItem.branchType == GitConstants.GIT_LOCAL_BRANCH_TYPE) {
					configureUpstreamGroup.visible = true;
					
					configureUpstreamCheckBox.selected = false;
					
					rebaseCommitsRadioButton.enabled = false;
					mergeUpstreamRadioButton.enabled = false;
				} else if (selectedItem.branchType == GitConstants.GIT_REMOTE_BRANCH_TYPE) {
					configureUpstreamGroup.visible = true;
					
					configureUpstreamCheckBox.selected = true;
					
					rebaseCommitsRadioButton.enabled = true;
					mergeUpstreamRadioButton.enabled = true;
				}
			}
			
		]]>
	</fx:Script>
			
	<s:Form>
		<s:FormItem label="Source:">
			<s:DropDownList id="selectBranch" 
							requireSelection="true"
							dataProvider="{branches}"
							change="selectBranchChangeHandler(event)"
							labelField="branchName"
							baseline="15"
							width="300"/>
		</s:FormItem>
		
		<s:FormItem label="Branch Name:">
			<s:TextInput id="branchNameTextInput"
						 baseline="15"
						 width="{selectBranch.width}"/>
		</s:FormItem>
	</s:Form>
	
	<s:VGroup id="configureUpstreamGroup">
		<s:CheckBox id="configureUpstreamCheckBox"
					label="Configure upstream for push and pull" 
					click="configureUpstreamClickHandler(event)"/>
		<s:Label text="When doing a pull"/>
		<s:RadioButton id="mergeUpstreamRadioButton"
					   label="Merge upstream commits into local branch"
					   click="rebaseCommitsRadioButton.selected=false;"/>
		<s:RadioButton id="rebaseCommitsRadioButton"
					   label="Rebase commits of local branch onto upstream"
					   click="mergeUpstreamRadioButton.selected=false;"/>
	</s:VGroup>
	
	<s:CheckBox id="checkoutBranchCheckBox"
				label="Checkout new Branch"/>

</ns:BasicViewContent>
