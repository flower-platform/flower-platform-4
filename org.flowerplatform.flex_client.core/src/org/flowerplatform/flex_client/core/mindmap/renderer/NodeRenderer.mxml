<?xml version="1.0" encoding="utf-8"?>
<!-- 
license-start

Copyright (C) 2008 - 2013 Crispico, <http://www.crispico.com/>.

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation version 3.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details, at <http://www.gnu.org/licenses/>.

Contributors:
Crispico - Initial API and implementation  The product is licensed under GPL. This is the new version of the license.

license-end
-->
<!---
@author Cristina Constantinescu
-->
<s:DataRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 implements="org.flowerplatform.flexdiagram.renderer.IDiagramShellAware" 
		 initialize="initializeHandler(event)" cacheAsBitmap="true">
		
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayList;
			import mx.collections.IList;
			import mx.core.DPIClassification;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			import mx.events.PropertyChangeEvent;
			import mx.events.ResizeEvent;
			
			import org.flowerplatform.flex_client.core.mindmap.Diagram;
			import org.flowerplatform.flex_client.core.mindmap.MindMapEditorDiagramShell;
			import org.flowerplatform.flex_client.core.mindmap.remote.Node;
			import org.flowerplatform.flexdiagram.DiagramShell;
			import org.flowerplatform.flexdiagram.event.ZoomPerformedEvent;
			import org.flowerplatform.flexdiagram.mindmap.MindMapDiagramShell;
			import org.flowerplatform.flexutil.FlexUtilGlobals;
			import org.flowerplatform.flexutil.FlowerArrayList;
			
			import spark.core.ContentCache;
			import spark.primitives.BitmapImage;
			
			private static const circleRadius:int = 3;
			
			private var _diagramShell:MindMapEditorDiagramShell;
								
			protected var iconDisplays:IList;
			
			private var _icons:FlowerArrayList = new FlowerArrayList();
			
			private static var imageCache:ContentCache;
			
			public function get icons():FlowerArrayList {
				return _icons;
			}
						
			public function set icons(value:FlowerArrayList):void {
				if (value == _icons)
					return;
				if (value == null) {
					_icons.removeAll();
				} else {
					var i:int;
					var j:int = 0;
					if (_icons.length > 0 && value.length > 0) {
						while (j < value.length && j < _icons.length) {
							BitmapImage(iconDisplays.getItemAt(j)).source = value.getItemAt(j);
							j++;
						}
					}
					if (j < _icons.length) {
						i = j;
						while (i < iconDisplays.length)  {
							removeIconDisplay(BitmapImage(iconDisplays.getItemAt(i)));
						}
					}
					if (j < value.length) {
						for (i = j; i < value.length; i++) {
							addIconDisplay(value.getItemAt(i));
						}
					}
					_icons = value;
				}			
			}
			
			protected function addIconDisplay(icon:Object):void {
				var iconDisplay:BitmapImage = new BitmapImage();
				iconDisplay.contentLoader = imageCache;
				iconDisplay.source = icon;
				
				addElementAt(iconDisplay, numElements - 1);
								
				if (iconDisplays == null) {
					iconDisplays = new ArrayList();
				}
				iconDisplays.addItem(iconDisplay);
			}

			protected function removeIconDisplay(iconDisplay:BitmapImage):void {
				removeElement(iconDisplay);
				iconDisplays.removeItemAt(iconDisplays.getItemIndex(iconDisplay));
				
				invalidateSize();
			}			
			
			public function get diagramShell():DiagramShell {
				return _diagramShell;
			}
			
			public function set diagramShell(value:DiagramShell):void {
				_diagramShell = MindMapEditorDiagramShell(value);
			}
			
			private function get node():Node {
				return Node(data);
			}
			
			public function get applicationDPI():Number	{
				return FlexGlobals.topLevelApplication.applicationDPI;
			}
						
			override public function set data(value:Object):void {
				if (super.data != null) {
					data.removeEventListener(PropertyChangeEvent.PROPERTY_CHANGE, modelChangedHandler);
//					View(data).removeEventListener(DiagramEditorStatefulClient.VIEW_DETAILS_UPDATED_EVENT, viewDetailsUpdatedHandler);					
				}
				
				super.data = value;
				
				if (data != null) {
					data.addEventListener(PropertyChangeEvent.PROPERTY_CHANGE, modelChangedHandler);
//					View(data).addEventListener(DiagramEditorStatefulClient.VIEW_DETAILS_UPDATED_EVENT, viewDetailsUpdatedHandler);
					
					x = _diagramShell.getModelController(node).getX(node);	
					y = _diagramShell.getModelController(node).getY(node);	
					
					labelDisplay.text = data.body;
//					if (node.viewDetails != null) {
//						viewDetailsUpdatedHandler(null);
//					}
				}
			}
				
			protected function initializeHandler(event:FlexEvent):void {				
				addEventListener(ResizeEvent.RESIZE, resizeHandler);
				addEventListener(ZoomPerformedEvent.ZOOM_PERFORMED, zoomPerformedHandler);
				
				if (imageCache == null) {
					imageCache = new ContentCache();
					imageCache.enableCaching = true;
					imageCache.maxCacheEntries = 100;
				}
				
				if (!FlexUtilGlobals.getInstance().isMobile) {
					minHeight = 22;
				} else {
					switch (applicationDPI) {
						case DPIClassification.DPI_320:	{
							minHeight = 88;
							break;
						}
						case DPIClassification.DPI_240:	{
							minHeight = 66;
							break;
						}
						default: {
							// default PPI160
							minHeight = 44;
							break;
						}
					}
				}
				
				setStyle("verticalGap", 0);
				setStyle("iconsGap", 1);
			}
					
			private function resizeHandler(event:ResizeEvent):void {
				_diagramShell.getModelController(node).setWidth(node, width);
				_diagramShell.getModelController(node).setHeight(node, height);
														
				var parent:Object = _diagramShell.getControllerProvider(node).getModelChildrenController(node).getParent(node);
				if (parent is Node) {						
					_diagramShell.refreshNodePositions(parent);	
				} else { // refresh root node					
					_diagramShell.refreshNodePositions(Diagram(_diagramShell.rootModel).rootNode);
				}				
			}						
			
//			protected function viewDetailsUpdatedHandler(event:Event):void {
//				if (node.viewDetails.hasOwnProperty("text")) {
//					labelDisplay.text = node.viewDetails.text;
//				}
//				
//				if (node.viewDetails.hasOwnProperty("minWidth")) {
//					minWidth = node.viewDetails.minWidth;
//				}
//				if (node.viewDetails.hasOwnProperty("maxWidth")) {
//					maxWidth = node.viewDetails.maxWidth;
//				}			
//							
//				if (node.viewDetails.hasOwnProperty("icons")) {
//					var nodeIcons:ArrayCollection = node.viewDetails.icons;
//					var iconsURLs:Array = new Array();
//					for (var i:int = 0; i < nodeIcons.length; i++) {
//						iconsURLs.push(FlexUtilGlobals.getInstance().createAbsoluteUrl(MindMapModelPlugin.getInstance().getResourceUrl(String(nodeIcons.getItemAt(i)))));
//					}
//					this.icons = new FlowerArrayList(iconsURLs);
//				}
//				invalidateSize();
//				invalidateDisplayList();
//			}
			
			private function modelChangedHandler(event:PropertyChangeEvent):void {
				switch (event.property) {
					case "x":
						x = _diagramShell.getModelController(node).getX(node);					
						break;
					case "y":
						y = _diagramShell.getModelController(node).getY(node);				
						break;
					case "body":
						labelDisplay.text = data.body;
					case "hasChildren":
						invalidateSize();
						invalidateDisplayList();
				}
			}
					
			protected function zoomPerformedHandler(event:ZoomPerformedEvent):void {
				invalidateSize();				
				invalidateDisplayList();
			}
			
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void {				
				super.updateDisplayList(unscaledWidth, unscaledHeight);
				
				graphics.clear();
				graphics.lineStyle(1, 0x808080);
				graphics.beginFill(0xCCCCCC, 0);
				graphics.drawRoundRect(0, 0, unscaledWidth, unscaledHeight, 10, 10);		
				
				if (node != null && node.hasChildren && !_diagramShell.getModelController(node).getExpanded(node)) {
					if (node.side == MindMapDiagramShell.LEFT) {
						graphics.drawCircle(-circleRadius, height/2, circleRadius);
					} else if (node.side == MindMapDiagramShell.RIGHT) {						
						graphics.drawCircle(width + circleRadius, height/2, circleRadius);
					}
				}
			}
			
			override public function validateDisplayList():void {
				super.validateDisplayList();
				
				if (iconDisplays && iconDisplays.length > 0) {
					for (var i:int=0; i < iconDisplays.length; i++) {
						var iconDisplay:BitmapImage = BitmapImage(iconDisplays.getItemAt(i));
						iconDisplay.validateDisplayList();
					}
				}
			}
			
			override public function validateProperties():void {
				super.validateProperties();
				
				if (iconDisplays && iconDisplays.length > 0) {
					for (var i:int=0; i < iconDisplays.length; i++) {
						var iconDisplay:BitmapImage = BitmapImage(iconDisplays.getItemAt(i));
						iconDisplay.validateProperties();
					}
				}
			}
			
			override public function validateSize(recursive:Boolean = false):void {
				if (iconDisplays && iconDisplays.length > 0) {
					for (var i:int=0; i < iconDisplays.length; i++) {
						var iconDisplay:BitmapImage = BitmapImage(iconDisplays.getItemAt(i));
						iconDisplay.validateSize();
					}
				}				
				super.validateSize(recursive);
			}
						
		]]>
	</fx:Script>
	<s:layout>
		<s:HorizontalLayout gap="2" verticalAlign="middle" paddingBottom="2" paddingTop="2" paddingLeft="2" paddingRight="2"/>		
	</s:layout>
	
	<s:Label id="labelDisplay" />
	
</s:DataRenderer>
